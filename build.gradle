group 'emt'
version '1.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'

def agent_archive = "test-agent.jar"

buildscript {
    repositories {
        mavenCentral()
        // The next repo is only needed while using SNAPSHOT versions
        //maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
    dependencies {
        classpath "net.saliman:gradle-cobertura-plugin:3.0.0"
    }
}
apply plugin: 'net.saliman.cobertura'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    // NOTE: Must stick to Groovy 2.0.8 as Cobertura cannot generate valid
    // coverage reports for newer versions. More details at:
    // https://github.com/cobertura/cobertura/issues/184
    implementation 'org.codehaus.groovy:groovy-all:2.0.8'
    implementation 'org.eclipse.hudson:hudson-core:3.2.1'
    implementation 'com.cloudbees:groovy-cps:1.22'
    testImplementation group: 'junit', name: 'junit', version: '4.13'
    testImplementation "org.mockito:mockito-core:2.+"
    testImplementation group: 'com.google.guava', name: 'guava', version: '23.5-jre'
    testImplementation 'net.bytebuddy:byte-buddy:LATEST'

}

sourceSets {
    main {
        java {
            srcDirs = []
        }
        groovy {
            srcDirs = ['src', 'vars']
        }
    }
    test {
        java {
            srcDir 'test'
        }
    }
}

tasks.register('agentJar', Jar) {
    description 'Assembles a jar archive containing the Java agent.'
    
    dependsOn 'compileTestJava', 'compileTestGroovy'
    
    archiveName = agent_archive

    from(['build/classes/java/test/com/emt/util/MyAgent.class'])

    manifest {
        attributes(
                'Premain-Class': 'com.emt.util.MyAgent',
                'Can-Redefine-Classes': 'true',
                'Can-Retransform-Classes': 'true',
                'Can-Set-Native-Method-Prefix': 'true',
                'Implementation-Title': "ClassLogger",
                'Implementation-Version': rootProject.version
        )
    }

}

test {
    dependsOn 'agentJar'

    jvmArgs "-javaagent:$buildDir/libs/${agent_archive}"
}

cobertura {
    coverageIncludes = ['.*com.emt.*']
    coverageFormats = ['html', 'xml']
    coverageIgnoreMethodAnnotations = ['com.emt.steps.CoverageIgnore']
}

// NOTE: Disable groovy optimizations to get more accurate Cobertura results
// See https://pbetkier.github.io/2014/08/07/groovy-code-coverage-issues.html
gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(':cobertura')) {
        compileGroovy.groovyOptions.optimizationOptions.all = false
    }
}